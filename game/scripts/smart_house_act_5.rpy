label smart_house_act_5_intro:
    ### ACT 5: THE OSTENSIBLE CALAMITY
    show black
    typing "September 13th. 10:24 P.M.\nSmart House - Kitchen"
    show kitchen with fade
    drang "This is rich. Now you're saying you had nothing to do with this whole thing."
    drang "And the only reason you confessed is because your brain computer told you to."
    bottomi "Uh...basically?"
    drang "I love this so much."
    drang "You see, back at the office, me and the boys have this whiteboard where we write the dumbest excuses crooks have told us."
    drang "\"My twin did it!\" \"I was possessed by a vengeance ghost!\" That kind of thing."
    drang "Every month we have a little voting party where the worst excuse wins twenty bucks."
    drang "And buddy, you just won me twenty bucks."
    drang "I'm gonna get me a nice fancy dinner thanks to you."
    warren "You don't believe him, then."
    drang "Of course I don't! It's ridiculous!"
    drang "Consider the two explainations here..."
    drang "Either somebody has been hacking this man's brain for the past two hours in order to make him falsely confess..."
    drang "{i}Or{/i} he killed a guy, fessed up, then got cold feet about going to jail."
    warren "I agree the whole thing sounds stupid, but it explains a lot of things."
    warren "The general confusion, the innacuracies, the way his story kept changing..."
    drang "If every guy who couldn't keep his story straight was being mindhacked, then half the guys I've locked up should have a brainchip."
    warren "Look, it can't hurt to investigate this."
    warren "He said his neural implant was relegated to the base's local network, so the hacker must be somewhere in Base 24."
    drang "Great. That narrows the suspects down to, what, a thousand?"
    warren "Let's start by asking Bottomi some questions. He might be able to clarify some things for us."
    warren "And if his story still doesn't add up, we can put him right back on the suspect list."
    drang "Fine. Go ahead."
    drang "In the meantime, I'm going to call my buddies and get them to put this up on the whiteboard."
    "* bleep *"
    drang "Hey! Gordo! Wait until you get a load of {i}this{/i} one!"
    warren "Mr Bottomi, are you feeling well enough to answer some questions?"
    bottomi "S-sure. I'm actually feeling better than I have for a while now..."
    menu:
        "Did You Do It?":
            warren "Why don't we get this one out of the way first:"
            warren "Did you, Louis Bottomi, kill Orin Darsha!"
            bottomi "No. I'm certain of that now."
            bottomi "I visited the Smart House earlier today, that much is true."
            bottomi "But Mr Darsha was already dead when I arrived."
            warren "Why did you go there?"
            bottomi "I... I don't know, come to think of it."
            bottomi "I just suddenly got to thinking that it would be a good idea to check out the kitchen."
            bottomi "H-hey, you don't think I was already being controlled at that point, do you?"
            bottomi "I {i}did{i/} think it was weird how excited I was to splash around in that mud..."
            warren thought "!"
            warren thought "Those footprints..."
            warren thought "Could this mystery culprit have been clever enough to fake incriminating evidence against Mr Bottomi?"
        "Your Confession":
            warren "So, you think somebody else is responsible for your confession earlier?"
            bottomi "Pretty much, yeah..."
            ash "Wow! So they, like, mind controlled you!"
            bottomi "Uh... not exactly..."
            bottomi "It's more like... they gave me fake memories of committing the crime."
            bottomi "From my perspective, it really seemed like I had done it."
            warren "It's fairly lucky for them that you had the integrity to confess."
            warren "I suspect most people would have tried to cover up their crime."
            warren "In fact, it would seem one such person {i}did.{/i}"
            bottomi "I guess that was pretty stupid of me, huh?"
            warren "Not at all. We'd be a lot better off if there were more people like you in the world."
            warren "Are you now able to tell which of your memories are real and which are false?"
            bottomi "Yes, I think so. Now that my connection to the implant is broken, the fake ones are starting to fade."
            bottomi "Sort of like how a dream seems so real in the moment, but you start to forget it when you wake up."
            ### Unlock Conflicting Stories
        "Conflicting Stories":
            warren "Why did your story keep changing so much?"
            bottomi "Well, my memories kept changing."
            bottomi "At the time I thought I had just been remembering things wrong..."
            bottomi "But now I think whoever did this was making new memories to counter your arguments."
            bottomi "Whenever you pointed out a flaw in my story, it genuinely hurt my head."
            bottomi "From my perspective, it was like you had just proven my own memories false."
            bottomi "It would seem like reality as I knew it was falling apart..."
            bottomi "And then, just like that, I would remember something new that explained everything."
            bottomi "It was almost a relief, even though it confirmed that I was the killer."
            warren "I see. But - wait a minute..."
            warren "If the true culprit was counter-programming you against my arguments..."
            warren "Doesn't that mean they were listening in on our conversation?"
            bottomi "W-well, hey, you're right!"
            bottomi "But wait... it's just us in the house, right? How could someone be listening?"
            warren "If they had access to your brain, then maybe they were listening through your ears..."
            ash "Or maybe they've also got access to the surveilance equipment built into this house."

    warren "Thank you, Lou. This has been very helpful."
    guard "Excuse me! Sheriff Warren!"
    warren "You again?"
    guard "Ms Angela Neering wishes to speak with you regarding the murder!"
    warren "Oh, of course. Where is she?"
    guard "Don't worry! I'll bring her to you!"
    warren "Uh, thanks."
    warren thought "Wait... \"bring her to you\"?"
    # Guard wheels in TV
    warren "What in the..."
    # TV turns on, no picture
    neering "Hello, Sherrif."
    neering "I've come because I thought I could - wait, can you see me?"
    warren "I'm sorry... what am I supposed to be seeing here?"
    neering "GUARD, GET OVER HERE! YOU SET THE TV TO THE WRONG INPUT, YOU MORON!"
    guard "Sorry! Sorry!"
    guard "I don't get it! I have it set to HDMI 1 and everything..."
    neering "Well keep trying one until it works!"
    guard "Oh! There we go!"
    neering "Great. Now get out of my sight!"
    guard "Y-yes, ma'am!"
    neering "Now, Sherrif. You can see me now, yes?"
    warren "I see a mid-2000's av club setup with your face on it, if that's what you mean."
    warren "I was under the impression that you would be coming to speak with me {i}in person{/i}."
    neering "TYM, I suppose!"
    warren "TYM?"
    neering "That's Your Mistake."
    neering "See, as much as I wish to help with your investigation, I'm still incredibly busy fixing this house."
    neering "I'm sure you understand that my life's work is a little bit more important."
    warren "Than solving a {i}murder?{/i}"
    neering "Well, see, here's the thing about that..."
    warren thought "Oh boy, here we go."
    warren "Agent Drang, I think you're going to want to hear this."
    drang "What is it? Are you going to tell me that this Television Woman is the killer?"
    neering "Well... the thing is..."
    neering "I'm... sorta.... the one who hacked that guy's neural implant..."
    drang "WHAAAAT!?"
    drang "YOU MEAN HIS ASININE FAKE MEMORIES EXCUSE WAS REAL?"
    neering "Yyyeaaaahhhhhh....."
    drang "I need a drink."
    drang "Hey! Medicine Man! Whatshisface!"
    carlos "Who, me?"
    drang "Give me that!"
    carlos "No! My Marg!"
    drang " * gulp *  * gulp *  * gulp *"
    drang " * spfffphhffhhpth * "
    drang "WHAT THE HELL WAS THAT?"
    carlos "Uh, a margarita. What do you think it was?"
    drang "B-but there's no tequila in this!"
    drang "Have... HAVE YOU BEEN DRINKING STRAIGHT MARGARITA MIX THIS ENTIRE TIME!?"
    carlos "You didn't think I was really drinking on the job, did you?"
    drang "Somehow... this is actually worse than if you {i}were{/i}..."
    warren "Ms Neering... I'm not sure if you understand what you just said..."
    warren "If you're the one who made Louis Bottomi confess to the murder..."
    warren "...Wouldn't that make {i}you{/i} the true killer?"
    neering "What? No. No no no no."
    neering "See, there's something you don't know about Orin Darsha's death."
    neering "The truth is... it was {color=red}an accident.{/color}"
    warren "An accident? What, did he fall down the stairs?"
    neering "No, nothing like that. It was... well... it was a malfunctioning piece of tech. Here in the house."
    warren "Oh, do you mean..."
    menu:
        "...The Grabber Hands?":
            warren "...The Grabber Hands?"
            neering "No. Although, not to brag or anything, they certainly {i}could{/i} kill a man."
            warren "That is... an interesting thing to brag about."
            neering "What can I say? I'm proud of my children."
            neering "No, it was The Dressing Contraption where poor Mr Darsha met his end."
            warren thought "That's right... we found that shoe which proved he was in there."
        "...The Dressing Contraption?":
            warren "...The Dressing Contraption?"
            warren "After all, we found his shoe in there while investigating."
            neering "Yes, that is the fateful place where poor Mr Darsha met his end."
        "... The Security System?":
            warren "... The Security System?"
            neering "No. Although, not to brag or anything, it certainly {i}could{/i} kill a man."
            warren "That is... an interesting thing to brag about."
            neering "What can I say? I'm proud of my children."
            neering "No, it was The Dressing Contraption where poor Mr Darsha met his end."
            warren thought "That's right... we found that shoe which proved he was in there."

    warren "But how is a glorified wardrobe even capable of killing a man?"
    neering "Hey, any part of a house can be dangerous!"
    neering "And, as it turns out, a machine which forces clothes onto a human body is also dangerous..."
    neering "But, I mean, it was only a teensy weensy rounding error."
    neering "How was I supposed to know that something like that would be enough to make it break his neck?"
    warren "I...see."
    warren "Carlos, can I talk to you over here for a second?"
    carlos "Sure, Chief."
    neering "?"
    warren "I don't know if Ms Neering is aware of this, but she's already confessed to several crimes."
    warren "Maybe she thinks that since Mr Darsha's death was an accident, she isn't culpable."
    warren "But since it's her own invention which killed him, it counts as criminally negligent manslaughter."
    warren "Not to mention the fact that she tampered with a crime scene and obstructed a murder investigation."
    carlos "Shoot, you're right."
    carlos "Well, we can't tell her now. She might bolt before we have a chance to arrest her."
    warren "Exactly. That's why I need you to go find her."
    carlos "Me?"
    warren "I've got to stay here and keep her talking so that you can track her down."
    warren "You're the only one I can trust with this."
    carlos ". . . I won't let you down, Chief."
    carlos os "BOY DO I HAVE TO GO TO THE BATHROOM"
    carlos os "LET ME JUST DUCK OUT AND MAKE MY WAY THERE"
    carlos os "TO THE BATHROOM, THAT IS"
    carlos os "WHICH IS WHERE I'M GOING"
    warren "* sigh *"
    neering "He better not go in any of the Smart House bathrooms..."
    neering "The pipes aren't actually connected to anything yet."
    drang "THEY AREN'T!?"
    neering "No, they're not. Why do you ask?"
    drang ". . . No reason."
    warren "Ms Neering, can you tell me more about this accident?"
    neering "WYS."
    menu:
        "Where's Your Soviergnity?":
            warren "Where's Your Soviergnity?"
            neering "What? No! What would that even mean?"
            warren "You know, like, what's your authority here?"
            warren "I thought maybe you were asking if I was in charge here."
            neering "No. Obviously it was \"Whatever You Say\"."
            warren "Fine. Sheesh."
            neering "Y'all's need to get on my level."

        "Whatever You Say?":
            warren "Whatever You Say?"
            neering "Yes! Thank you!"
            neering "Finally someone here speaks plain english!"
            warren thought "Honestly, that was a complete shot in the dark."
            warren thought "Glad it worked out, I guess."

        "Cram it with the acronyms already!":
            warren "Cram it with the acronyms already!"
            neering "I'm not going to let some mouth-breather lower my language to their level."
            neering "Either figure out what I'm saying on your own, or let me explain it to you."
            neering "And by the way, it was \"Whatever You Say\"."
            warren thought "See, how on earth was I supposed to guess that?"

    # Eyewitness Statement ANIMATION
    typing "-- my_testimony.txt --{fast}"
    neering "After the tour, Darsha came to ash me a few questions about the Smart House."
    neering "He, of his own volition, decided to test out The Dressing Contraption."
    neering "Needless to say, it didn't go so well for him."
    neering "I knew that an accident on the day of the Smart House's unveiling would be bad press."
    neering "So I framed that buffoon with the neural implant and tricked him into confessing."

    #fade out, fade in
    ash "All right, what's our M.O. right now?"
    warren "We just need to keep her talking long enough that Carlos can figure out where she's holed up."
    warren "And, if possible, find any holes in her story."
    ash "But shouldn't Ms Neering's statement here be \"the truth\", so to speak?"
    warren "See, I thought so too... but something in her story doesn't line up for me."
    ash "Okay, let's see if we can figure out what that is!"
    drang "WARREN!"
    drang "YOU BETTER FIX THIS, QUICK!"
    warren "F-fix what?"
    drang "This... this whole case!"
    drang "We had everything wrapped up all neat and tidy!"
    drang "But then you just had to go picking at every little scab you saw..."
    drang "And now we got computer women and mind control and all this other crap!"
    drang "DO YOU KNOW HOW MUCH PAPERWORK I'M GOING TO HAVE TO DO NOW?"
    drang "I HAVE TO FILL OUT THREE EXTRA FORMS ALONE, JUST FOR THE MIND CONTROL!"
    warren "Your office has specific paperwork related to mind control?"
    drang "OF COURSE WE DO!"
    drang "IT'S THE FBI, ISN'T IT?"
    drang "NOW BRING ME SOMEONE TO ARREST BEFORE I JUST PICK ONE AT RANDOM!"
    warren thought "Boy, I'll bet internal affairs would have a field day with this guy..."

$ current_present = "SH_Testimony8B"
$ back_action = "CurrentTestimony"

label SH_Testimony8A:
    $ settesti("SH_Testimony8A", None, "SH_Testimony8B", "SH_Press8A","SH_Advice8")
    show screen testi
    neering "After the tour, Darsha came to ash me a few questions about the Smart House."
    jump SH_Testimony8B

label SH_Press8A:
    hide screen testi
    warren "Shouldn't the tour have told him everything he needed to know?"
    neering "That tour was more geared towards... pedestrians."
    neering "A lot of concepts were dumbed down so that your average simpleton could understand them."
    neering "It's a dreary yet neccesary process when selling to... {i}the common masses.{/i}"
    warren thought "You didn't need to shudder like that when you said \"the common masses\"."
    warren thought "As a common mass myself, I take offense to that."
    warren "What kind of things was Mr Darsha asking about?"
    neering "Money, of course."
    neering "How much does each A.R.M. cost? What's the price-to-benefit ratio for each feature?"
    neering "It's exhausting!"
    neering "I build that man a revolution in home living and he wants to know where we can cut costs."
    neering "Would you cut costs... on a sunset?"
    warren thought "I swear I must have run into the annual narcissist convention or something..."
    jump SH_Testimony8B

label SH_Testimony8B:
    $ settesti("SH_Testimony8B", "SH_Testimony8A", "SH_Testimony8C", "SH_Press8B","SH_Advice8")
    show screen testi
    neering "He, of his own volition, decided to test out The Dressing Contraption."
    jump SH_Testimony8C

label SH_Press8B:
    hide screen testi
    warren "You sure seem, uh, {i}insistent{/i} that it was Darsha's own decision to use the contraption."
    warren "Why is that?"
    neering "I just don't want anyone here thinking that I {i}made{/i} Darsha do anything."
    neering "Otherwise I just know some clown at the insurance agency is gonna be like,"
    neering "\"Oh, you were the one who made him try it out, so it's your fault that he died!\""
    neering "So let's just set the record straight, here and now: Darsha {i}wanted{/i} to test it."
    neering "He was all riled up about how he didn't get to try it during the tour, so he made me run him through it."
    neering "If I'd had time beforehand, I could have noticed the bug in the contraption and fixed it."
    neering "This is only a prototype, after all. It's not fully polished."
    neering "So really it's on him that he died, not me."
    warren thought "That is... {i}super{/i} not how culpability works."
    warren thought "And besides, did Darsha {i}really{/i} intend to go through with that test?"

    jump SH_Testimony8C

label SH_Testimony8C:
    $ settesti("SH_Testimony8C", "SH_Testimony8B", "SH_Testimony8D", "SH_Press8C","SH_Advice8")
    show screen testi
    neering "Needless to say, it didn't go so well for him."
    jump SH_Testimony8D

label SH_Press8C:
    hide screen testi
    warren "Uh, care to elaborate on that point?"
    neering "Sure, but it's very technical... and graphic."
    neering "Basically what happened is there was a slight rounding error in the contraption's algorithm."
    neering "An easily repairable mistake, if I'd just been given time to troubleshoot."
    neering "As a result, it mistook Mr Darsha's skull for a nightcap which needed to be removed."
    neering "I didn't see any of this, mind you."
    neering "This is all extrapolated from the state of his body, as well as my own knowledge of the algorithm."
    neering "It's just as well I didn't see any of it, because this next bit gets a little grisly."
    neering "Essentially, in attempting to remove this \"nightcap\", the contraption dislocated his cervical vertebrae."
    neering "That means it broke his neck, by the way."
    warren "I know what a cervical vertebrae is."
    neering "Now, you already know that the house's sensors can't recognize a human after they've died."
    neering "So when the house realized it was holding unfamiliar matter, it executed its error routine..."
    neering "Which, in this case, is to drop the foriegn matter through the exit hatch."
    warren "Well, that certainly explains the bruises and the broken neck..."
    warren "But what about the victim's stab wound?"
    neering "I'm getting to that!"
    jump SH_Testimony8D

label SH_Testimony8D:
    $ settesti("SH_Testimony8D", "SH_Testimony8C", "SH_Testimony8E", "SH_Press8D","SH_Advice8")
    show screen testi
    neering "I knew that an accident on the day of the Smart House's unveiling would be bad press."
    jump SH_Testimony8E

label SH_Press8D:
    hide screen testi
    warren "That's all it was to you? Bad press?"
    neering "Well, think about it from my perspective."
    neering "This house... is my life's work."
    neering "It is the culmination of all of my studies in machine learning, bionic engineering, and interior design."
    neering "And today was the day where I would finally unveil the fruits of my labor to the world."
    neering "How could I let something so trivial as an accidental fatality ruin this moment?"
    neering "If the press finds a dead body in the kitchen of my invention, what's the headline going to be tomorrow?"
    neering "Is it going to be \"Amazing Genius Improves Everyone's Lives With Great Invention\"?"
    neering "Or is it going to be \"Dangerous House Does A Murder, Is Broken\"?"
    neering "I just {i}tweaked{/i} the headline to \"Crazy Guy Kills Boss In Super Cool House\"."
    neering "This is what we in the business like to call \"Public Relations\"."
    warren thought "I'll be sure to write \"Wanted for Public Relations\" on the arrest warrent, then."
    jump SH_Testimony8E

label SH_Testimony8E:
    $ settesti("SH_Testimony8E", "SH_Testimony8D", "SH_Testimony8A", "SH_Press8E","SH_Advice8")
    show screen testi
    neering "So I framed that buffoon with the neural implant and tricked him into confessing."
    jump SH_Testimony8A

label SH_Press8E:
    hide screen testi
    warren "So you're the one that stuck the knife in the victim's back?"
    neering "No, no, of course not."
    neering "I had the house do it."
    warren "But what about the footprints made by Louis Bottomi?"
    neering "Easy. I splashed some mud around outside the house..."
    neering "Then I \"convinced\" the stooge to take a stroll around."
    neering "It was child's play, really."
    warren "But why such an elaborate ruse?"
    warren "Why bother with all of this if your deception was so full of contradictions?"
    neering "Well, to be honest, I didn't figure you cops would be quite so inquiring."
    neering "I figured if you saw a knife in someone's back and a guy saying \"I did it\"..."
    neering "Then you wouldn't bother yourself with anything that didn't quite line up."
    warren "I'm afraid that while your technique might have worked on {i}some people...{/i}"
    drang ". . . Hey! What are you looking at me for!?"
    warren "...it didn't quite work on me."
    neering "I'll keep that in mind for the next time I need to fabricate a crime scene. I guess."
    jump SH_Testimony8A

label SH_Advice8:
    hide screen testi
    ash "Okay, shoot. I'm stumped."
    ash "I feel like we should be looking for some flaw in her description of Mr Darsha's death."
    ash "Or maybe something with her cover-up of the whole thing."
    warren "It's much simpler than that, really."
    warren "We don't need to destroy Ms Neering's entire story. Not yet, at least."
    warren "All we need to do is answer one central question: {color=red}Did Orin Darsha really intend to use the contraption?{/color}"
    warren "If we can figure that out, we should be able to find the chink in the armor we need."
    ash "Got it!"
    jump CurrentTestimony

label SH_Objection8:
    hide screen testi
    if testipart == "SH_Testimony8B" and present_response == "Scratch Marks":
        jump SH_Success8
    else:
        jump SH_Failure8

label SH_Failure8:
    warren "I've got a piece of evidence here that's gonn{nw}"
    neering "Let me see that."
    neering ". . . ."
    neering "Using a complex detection algorithm, I've managed to find 16 flaws in your so-called piece of evidence."
    neering "Mathematically speaking, there is no way this has any bearing on my testimony whatsoever."
    neering "Why don't you just put that thing away? You're only embarrasing yourself."
    warren "ggghhhhAAAAAHHH!"
    $ mc_health -= 1
    warren "I can feel Drang's glare on me from halfway across the room."
    if mc_health == 0:
        jump SH_GameOver8
    else:
        jump SH_Testimony8A

label SH_GameOver8:
    drang "Aaaaand it looks like we're finished here."
    drang "I've been about as patient as one could reasonably expect from a man such as myself."
    drang "But now I'm just going to pick somebody at random and arrest them for something."
    drang "Eenie meenie miney mo..."
    ash "Aah! Let's get out of here!"
    warren "B-but we still haven't found out the truth here!"
    ash "Hey, I'm big on the truth too, but I don't know if getting locked up by this guy is worth it."
    warren "What about Mr Bottomi? Heck, what about Carlos?"
    drang "...my mother said to pick the very best one..."
    warren "Okay, let's go."
    show black with fade
    warren thought "And so, the truth disappeared into darkness."
    warren thought "Drang failed to bring the real culprit to justice."
    warren thought "And I never unravelled the enigma of Base 24."
    jump endgame

label SH_Success8:
    warren "You know, Ms Neering, I'd really hoped that breaking Mr Bottomi out of his trance would resolve this whole affair."
    warren "But it seems you are determined to draw this out as much as possible."
    neering "What are you babbling on about?"
    warren "You claim that Mister Darsha was fully willing to test out The Dressing Contraption."
    warren "Not just that, but that he himself was the one who insisted on trying it."
    warren "But why would a willing participant leave scratch marks on the carpet leading to the trap door?"
    neering "W-what's this now?"
    warren "Scratch marks. Bloody scratch marks leading up to the point where the floor opens up."
    warren "Now, the only way I can think to explain something like that..."
    warren "Is if someone were desperately attempting to avoid falling into The Dressing Contraption!"
    neering "Yipes!"
    warren "Woah. I didn't know that Ms Neering was {i}capable{/i} of being caught off guard."
    ash "Or that anybody could unironically say \"Yipes\"."
    warren "So, Angela... care to explain how those scratch marks got there?"
    neering "H-hah! Hah! You think you've made some brilliant deduction?"
    neering "Who's to say those scratch marks are from today?"
    warren "Huh?"
    neering "The Smart House project has been in development for years now."
    neering "That's hundreds of days during which somebody could have scratched up the carpeting."
    neering "You couldn't possibly have proof that those marks are recent."
    warren "As a matter of fact... I do."
    neering "IMPOSSIBLE!"
    neering "Well, I'm a woman of science. I should never call anything \"impossible\"."
    neering "STATISTICALLY IMPROBABLE!"
    warren "That may be, but the odds are in my favor tonight."

label scratchesProof:
    warren "Here is the proof that the scratches in the carpet are from today."
    $current_present = "scratchesProofPresent"
    show screen present_evidence_screen

label scratchesProofPresent:
    if present_response == "NeeringsEmail":
        jump scratchesProofSuccess
    else:
        neering "What is this. This is nothing. This is a clown's mistake."
        warren "No...you see... it proves that.......{nw}"
        $ mc_health -= 1
        warren "GWAAAAH!"
        neering "It seems the laws of statistics are on my side after all!"
        warren thought "Drat. I really thought I had that one."
        if mc_health == 0:
            jump SH_GameOver8
        else:
            warren "Let's give this one more shot."
            jump scratchesProof

label scratchesProofSuccess:
    warren "You must have been very busy the past few days, Ms Neering."
    neering "Why, yes. I have bee- oh, wait. This is a lead-in to whatever your point is."
    warren "Busy enough, it seems, to forget about this email exchange between you and Orin Darsha."
    neering "You went through my emails? You can't do that!"
    warren "We'll talk about what I can and cannot do later on."
    warren "For now, let's take a look at this particular section."
    warren "\"The new carpets will be ready in time for the tours. I've got contractors coming in the day before the event.\""
    neering "!"
    warren "As you can see, the scratch marks on the carpet must be from today..."
    warren "BECAUSE THE CARPETING WASN'T EVEN HERE BEFORE YESTERDAY!"
    neering "Noooooooooo!"
    neering "This... this doesn't change anything, you know!"
    neering "Orin Darsha's death was still an accident!"
    neering "And I'll prove it... with your precious \"testimony\"!"

    # Eyewitness Statement ANIMATION
    typing "-- my_testimony_FINAL.txt --{fast}"
    # There are many explainations for the scratches in the carpet.
    neering "TAMEFTSITC."
    # Perhaps one of the contractors installing the carpet did it by mistake.
    neering "POOTCITCDIBM."
    # Besides, a couple of scratch marks doesn't prove anything.
    neering "BACOSMDPA."
    # You want to pin this whole thing on me? Well, it's not gonna happen!
    neering "YWTPTWTOM? WINGH."
    # Darsha's death was just an accident caused by a mistake in the AI.
    neering "DDWJAACBAMITAI."

    # fade out, fade in
    warren ". . . . . . . . . ."
    warren "WHAT THE HELL WAS THAT?"
    neering "My testimony."
    warren "That wasn't a testimony! That was..."
    menu:
        "...bad dadaist poetry!":
            warren "...bad dadaist poetry!"
        "...syllable puke!":
            warren "...syllable puke!"
        "...a conlang invented by morons!":
            warren "...a conlang invented by a moron!"

    neering "As a matter of fact, it was a series of acronyms."
    neering "You've taken enough of my time as it is. I can't afford to waste time speaking every sentence."
    warren thought "Again with the acronyms! At this point, I think she's just doing it to tick me off."

    # Interrogation Animation
